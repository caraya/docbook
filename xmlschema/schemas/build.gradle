defaultTasks 'schemas'

buildscript {
  repositories {
    mavenCentral()
    maven { url "http://maven.restlet.org" }
  }

  dependencies {
    classpath group: 'org.docbook', name: 'docbook-xslt2', version: '2.4.0'
    classpath group: 'com.xmlcalabash', name: 'xmlcalabash1-gradle', version: '1.3.5'
  }
}

repositories {
  mavenLocal()
  mavenCentral()
}

apply plugin: 'org.docbook.task'
apply plugin: 'com.xmlcalabash.task'

import org.docbook.DocBookTask
import com.xmlcalabash.XMLCalabashTask

task docbook11_xsd(type: Copy) {
  from("docbook/docbook.xsd") {
    rename "docbook.xsd", "docbook-xsd11.xsd"
  }
  into "."
}

task docbook10_xsd(dependsOn: docbook11_xsd, type: XMLCalabashTask) {
  inputs.file "docbook-xsd11.xsd"
  inputs.file "../tools/11to10.xsl"
  outputs.file "docbook-xsd10.xsd"

  pipeline "../tools/xslt.xpl"
  input("source", "docbook-xsd11.xsd")
  input("stylesheet", "../tools/11to10.xsl")
  output("result", "docbook-xsd10.xsd")
}

task docbook_sch(dependsOn: docbook11_xsd, type: XMLCalabashTask) {
  inputs.file "docbook-xsd11.xsd"
  inputs.file "../tools/xsd-to-sch.xsl"
  outputs.file "docbook-xsd.sch"

  pipeline "../tools/xslt.xpl"
  input("source", "docbook-xsd11.xsd")
  input("stylesheet", "../tools/xsd-to-sch.xsl")
  output("result", "docbook-xsd.sch")
}

task schemas(dependsOn: ['docbook11_xsd',
                         'docbook10_xsd',
                         'docbook_sch']) {
  doLast {
    println("Generated schemas.")
  }
}

task clean {
  doFirst {
    delete fileTree(dir: '.').include("*.xsd")
    delete fileTree(dir: '.').include("*.sch")
  }
}
